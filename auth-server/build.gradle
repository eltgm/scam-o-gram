plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.1'
    id "org.openapi.generator" version "6.6.0"
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ru.sultanyarov'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':common')

    //DB
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core'
    runtimeOnly 'org.postgresql:postgresql'

    //Security
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //Util
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'com.google.guava:guava:32.0.1-jre'

    //Mapping support
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    //OpenApi generator
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    implementation "org.openapitools:jackson-databind-nullable:0.2.1"

    //AOP
    implementation 'org.springframework.boot:spring-boot-starter-aop'
}

openApiGenerate {
    verbose = false
    validateSpec = true
    generatorName = "spring"
    inputSpec = file('spec/auth.yaml').toPath().toString()
    apiPackage = 'ru.sultanyarov.authserver.auth'
    modelPackage = 'ru.sultanyarov.authserver.auth.dto'
    typeMappings = [
            OffsetDateTime: 'java.time.LocalDateTime',
            Error         : 'ru.sultanyarov.common.dto.Error'
    ]
    configOptions = [
            dateLibrary         : "java8",
            skipDefaultInterface: "true",
            interfaceOnly       : "true",
            oas3                : "true",
            useSpringController : "true",
            useSpringfox        : "false",
            useTags             : "true",
            useSpringBoot3      : "true"
    ]
}

sourceSets {
    main {
        java.srcDirs += "build/generate-resources/main/src/main/java"
        java.srcDirs += "$buildDir/generated/src/main/java"
    }
    test {
        java.srcDirs += "build/generate-resources/main/src/main/java"
        java.srcDirs += "$buildDir/generated/src/main/java"
    }
}

compileJava {
    options.compilerArgs << '-Amapstruct.defaultComponentModel=spring' << '-Amapstruct.unmappedTargetPolicy=IGNORE'
    dependsOn tasks.openApiGenerate
}

bootJar {
    archiveFileName = 'auth-server.jar'
    archiveExtension = '.jar'
}

tasks.named('test') {
    useJUnitPlatform()
}